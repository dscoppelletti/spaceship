
buildscript {
    ext.BOM = 'it.scoppelletti.spaceship:spaceship-bom:1.0.0'
    ext.kotlin_version = '1.3.71'

    repositories {
        jcenter()

        if (project.hasProperty('it.scoppelletti.tools.devRepoUrl')) {
            maven {
                url project.property('it.scoppelletti.tools.devRepoUrl')
            }
        }
    }
    dependencies {
        classpath platform(BOM)
        classpath 'com.github.ben-manes:gradle-versions-plugin'
        classpath 'it.scoppelletti.spaceship:spaceship-gradle-java'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin'
    }
}

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'it.scoppelletti.spaceship.spaceship-gradle-java'

group 'it.scoppelletti.spaceship'
version '1.0.0'
description 'Spaceship Standard Library'
archivesBaseName = 'spaceship-stdlib'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
}

spaceship {
    inceptionYear = '2019'
}

repositories {
    jcenter()
    mavenCentral()

    if (project.hasProperty('it.scoppelletti.tools.devRepoUrl')) {
        maven {
            url project.property('it.scoppelletti.tools.devRepoUrl')
        }
    }
}

configurations {
    bom
    api.extendsFrom(bom)
    kapt.extendsFrom(bom)
}

dependencies {
    bom platform(BOM)
    api 'com.google.dagger:dagger'
    kapt 'com.google.dagger:dagger-compiler'
    api 'com.squareup.okio:okio'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    api 'org.threeten:threetenbp::no-tzdb'
    implementation 'io.github.microutils:kotlin-logging'
    implementation 'org.slf4j:slf4j-api'
    testImplementation 'junit:junit'
    testImplementation "org.jetbrains.kotlin:kotlin-test:${kotlin_version}"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${kotlin_version}"
    testRuntimeOnly 'org.slf4j:slf4j-simple'
}
