apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion versionMap.sdk_target
    buildToolsVersion versionMap.build_tools
    defaultConfig {
        applicationId "it.scoppelletti.spaceship.ads.sample"
        minSdkVersion versionMap.sdk_min
        targetSdkVersion versionMap.sdk_target
        versionCode 1
        versionName versionMap.spaceship
        vectorDrawables.useSupportLibrary = true
        if (project.hasProperty('ads.serviceUrl')) {
            buildConfigField 'String', 'ADS_SERVICEURL',
                    '"' + project.property('ads.serviceUrl') + '"'
        } else {
            buildConfigField 'String', 'ADS_SERVICEURL', '"N/D"'
        }
        if (project.hasProperty('ads.publisherId')) {
            buildConfigField 'String', 'ADS_PUBLISHERID',
                    '"' + project.property('ads.publisherId') + '"'
        } else {
            buildConfigField 'String', 'ADS_PUBLISHERID', '"N/D"'
        }
        if (project.hasProperty('ads.adAppId')) {
            buildConfigField 'String', 'ADS_APPID',
                    '"' + project.property('ads.adAppId') + '"'
            manifestPlaceholders =
                    [ adsAppId : project.property('ads.adAppId') ]
        } else {
            buildConfigField 'String', 'ADS_APPID', '"N/D"'
        }
        if (project.hasProperty('ads.adUnitId')) {
            buildConfigField 'String', 'ADS_UNITID',
                    '"' + project.property('ads.adUnitId') + '"'
        } else {
            buildConfigField 'String', 'ADS_UNITID', '"N/D"'
        }
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'debug-rules.pro'
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation project(':lib')
    implementation "androidx.annotation:annotation:${versionMap.androidx_annotation}"
    implementation "androidx.appcompat:appcompat:${versionMap.androidx_appcompat}"
    implementation "androidx.browser:browser:${versionMap.androidx_browser}"
    implementation "androidx.preference:preference:${versionMap.androidx_preference}"
    implementation "com.google.android.material:material:${versionMap.androidx_material}"
    implementation "com.google.dagger:dagger:${versionMap.dagger}"
    kapt "com.google.dagger:dagger-compiler:${versionMap.dagger}"
    implementation "com.google.firebase:firebase-ads:${versionMap.firebase_ads}"
    implementation "com.google.firebase:firebase-core:${versionMap.firebase_core}"
    implementation "io.github.microutils:kotlin-logging:${versionMap.kotlin_logging}"
    implementation("it.scoppelletti.spaceship:spaceship-preference:${versionMap.spaceship}") {
        changing = true
    }
    implementation("it.scoppelletti.spaceship:spaceship-stdlib:${versionMap.spaceship}") {
        changing = true
    }
    implementation("it.scoppelletti.spaceship:spaceship-ui:${versionMap.spaceship}") {
        changing = true
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versionMap.kotlin}"
    implementation "org.slf4j:slf4j-api:${versionMap.slf4j}"
    runtimeOnly "com.noveogroup.android:android-logger:${versionMap.android_logger}"
}

apply plugin: 'com.google.gms.google-services'
