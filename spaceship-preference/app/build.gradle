apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion sdk_target
    defaultConfig {
        applicationId "it.scoppelletti.spaceship.preference.sample"
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        versionCode 1
        versionName '1.0.0'
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

configurations {
    bom
    implementation.extendsFrom(bom)
    kapt.extendsFrom(bom)
}

dependencies {
    bom platform(BOM)
    implementation project(':lib')
    implementation 'androidx.activity:activity-ktx'
    implementation 'androidx.annotation:annotation'
    implementation 'androidx.appcompat:appcompat'
    implementation 'androidx.fragment:fragment-ktx'
    implementation 'androidx.preference:preference-ktx'
    implementation "com.google.android.gms:play-services-oss-licenses:${oss_version}"
    implementation 'com.google.android.material:material'
    implementation 'com.google.dagger:dagger'
    kapt 'com.google.dagger:dagger-compiler'
    implementation 'it.scoppelletti.spaceship:spaceship-html'
    implementation 'it.scoppelletti.spaceship:spaceship-stdlib'
    implementation 'it.scoppelletti.spaceship:spaceship-ui'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    runtimeOnly 'com.noveogroup.android:android-logger'
}
